/*
 * SPDX-FileCopyrightText: 2023 Peter Hasse <peter.hasse@fokus.fraunhofer.de>
 * SPDX-FileCopyrightText: 2023 Fraunhofer FOKUS
 *
 * SPDX-License-Identifier: apache2
 */

plugins {
    id 'com.android.application'
    id "io.sentry.android.gradle" version "4.13.0"
    id 'androidx.navigation.safeargs'
    id("org.spdx.sbom") version "0.8.0"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('android/key.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    project.logger.warn("File not found: ${keystorePropertiesFile}")
    project.logger.warn("Using default values for signing config.")
    keystoreProperties['keyAlias'] = "omnt_default"
    keystoreProperties['keyPassword'] = "omnt2024"
    keystoreProperties['storeFile'] = file("omnt_default.jks")
    keystoreProperties['storePassword'] = "omnt2024"
}

android {
    signingConfigs {
        debug {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
        release{
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile keystoreProperties['storeFile'] ? file(keystoreProperties['storeFile']) : null
            storePassword keystoreProperties['storePassword']
        }
    }

    compileSdk 34

    defaultConfig {
        applicationId "de.fraunhofer.fokus.OpenMobileNetworkToolkit"
        minSdk 31
        targetSdk 34
        versionCode 4
        versionName "0.4"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    // We'll define the flavor dimensions here, and so far we only have one, for the server environment
    // this new way of adding dimensions is kinda weird tho
    flavorDimensions += ["environment"]

    productFlavors {
        // These are used in CloudCityParamsRepository, which - for now - defines our base server URL and token to use
        // which can also be altered through the app's settings at runtime
        // The whole a_, b_, c_ thing is so I can control the ordering, since they're lexicographically ordered
        // and we really don't want to default to the 'demo' flavor...
        a_staging {
            dimension "environment"
            buildConfigField "Boolean", "IS_DEMO", "false"
            buildConfigField "Boolean", "IS_STAGING", "true"
            buildConfigField "Boolean", "IS_PRODUCTION", "false"
        }
        b_demo {
            dimension "environment"
            buildConfigField "Boolean", "IS_DEMO", "true"
            buildConfigField "Boolean", "IS_STAGING", "false"
            buildConfigField "Boolean", "IS_PRODUCTION", "false"
        }
        c_production {
            dimension "environment"
            buildConfigField "Boolean", "IS_DEMO", "false"
            buildConfigField "Boolean", "IS_STAGING", "false"
            buildConfigField "Boolean", "IS_PRODUCTION", "true"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
        tasks.withType(JavaCompile).tap {
            configureEach {
                options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
            }
        }


    }
    buildFeatures {
        viewBinding true
        buildConfig = true
    }
    namespace 'de.fraunhofer.fokus.OpenMobileNetworkToolkit'
    ndkVersion '25.2.9519653'
    lint {
        abortOnError false
    }

    /**
     * NOTE: if changing this, make sure to update the expected filename in the android.yml workflow!
     */
    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach {
            def cleanFlavorName
            switch (variant.flavorName) {
                case "a_staging":
                    cleanFlavorName = "Staging"
                    break
                case "b_demo":
                    cleanFlavorName = "Demo"
                    break
                case "c_production":
                    cleanFlavorName = "Production"
                    break
                default:
                    cleanFlavorName = "Unknown"
            }

            outputFileName = "CloudCity_OpenMobileNetworkToolkit_${cleanFlavorName}_${variant.buildType.name}_${defaultConfig.versionName}.apk"
        }
    }
}

spdxSbom {
    targets {
        register("release") {
            configurations = ['releaseRuntimeClasspath']
            scm {
                uri.set("https://github.com/omnt/OpenMobileNetworkToolkit")
                revision.set("0.3")
            }
            document {
                name.set("OpenMobileNetworkToolkit")
                namespace.set("de.fraunhofer.fokus.OpenMobileNetworkToolkit")
                creator.set("Person: NGNI")
                packageSupplier.set("Organization: Fraunhofer FOKUS NGNI")
            }
        }
    }
}

// Sentry
sentry {
    // Enables more detailed log output, e.g. for sentry-cli.
    //
    // Default is false.
    debug = false
//    debug = true

    // Generates a source bundle and uploads it to Sentry.
    // This enables source context, allowing you to see your source
    // code as part of your stack traces in Sentry.
    //
    // Default is disabled. To enable, see the source context guide.
    includeSourceContext = true

    // Includes additional source directories into the source bundle.
    // These directories are resolved relative to the project directory.
//    additionalSourceDirsForSourceContext = ["mysrc/java", "other-source-dir/main/kotlin"] //We don't need this yet

    // Disables or enables dependencies metadata reporting for Sentry.
    // If enabled, the plugin will collect external dependencies and
    // upload them to Sentry as part of events. If disabled, all the logic
    // related to the dependencies metadata report will be excluded.
    //
    // Default is enabled.
    includeDependenciesReport = true

    // Automatically adds Sentry dependencies to your project.
    autoInstallation {
        enabled = true
    }
}

dependencies {
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'androidx.work:work-runtime:2.9.1'
    def room_version = "2.6.1"

    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.guava:guava:33.2.1-jre'
    implementation 'androidx.concurrent:concurrent-futures:1.2.0'
    implementation 'androidx.activity:activity:1.9.3'
    implementation 'androidx.fragment:fragment:1.8.4'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.8.3'
    implementation 'androidx.navigation:navigation-ui:2.8.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'androidx.recyclerview:recyclerview-selection:1.1.0'
    implementation 'com.influxdb:influxdb-client-java:7.2.0'
    implementation 'com.google.android.gms:play-services-location:21.3.0'
    implementation 'com.github.anastr:speedviewlib:1.6.1'
    implementation "androidx.viewpager2:viewpager2:1.1.0"
    implementation "androidx.compose.material3:material3:1.3.0"
    // OkHttp and Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
}

configurations.implementation {
    exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
}
